import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) * 100 + 4000000
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "cz.kutner.comicsdbclient.comicsdbclient"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        DdmPreferences.setTimeOut(60000)
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"

    implementation "com.google.firebase:firebase-core:$firebase_version"
    releaseImplementation  "com.google.firebase:firebase-crash:$firebase_version"
    implementation "com.google.firebase:firebase-perf:$firebase_version"

    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.github.jkwiecien:Switcher:2.1.5'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'co.metalab.asyncawait:asyncawait:1.0.0'

    implementation 'com.github.bumptech.glide:glide:4.2.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.2.0@aar'

    implementation 'com.squareup.okhttp3:okhttp:3.9.0'

    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.3-support26.1.0'
    implementation "com.mikepenz:iconics-core:2.9.5@aar"
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.4@aar'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.11.0'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'

    androidTestImplementation ('com.android.support.test:runner:1.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('com.android.support.test.espresso:espresso-contrib:3.0.0') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestImplementation ('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude module: 'okio'
        exclude module: 'support-annotations'
        exclude module: 'okhttp'
    }

    implementation 'org.koin:koin-android:0.5.1'

    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'com.jakewharton.timber:timber:4.6.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta3'

    kapt "com.android.databinding:compiler:$gradle_plugin_version"
}

apply plugin: 'com.google.gms.google-services'

