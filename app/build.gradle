import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) * 100 + 4000000
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "cz.kutner.comicsdbclient.comicsdbclient"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        DdmPreferences.setTimeOut(60000)
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    dataBinding {
        enabled = true
    }

    kapt {
        javacOptions {
            // Increase the max count of errors from annotation processors.
            // Default is 100.
            option("-Xmaxerrs", 500)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "androidx.appcompat:appcompat:$support_library_version"
    implementation "androidx.cardview:cardview:$support_library_version"
    implementation "androidx.recyclerview:recyclerview:$support_library_version"
    implementation "com.google.android.material:material:$support_library_version"
    implementation "androidx.core:core-ktx:$support_library_version"

    implementation "androidx.lifecycle:lifecycle-livedata:2.0.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel:2.0.0"

    implementation 'com.github.tukak:Switcher:2.4.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.30.1-eap13"

    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation 'com.squareup.picasso:picasso:2.71828'

    implementation 'com.squareup.okhttp3:okhttp:3.11.0'

    implementation 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation 'com.github.daniel-stoneuk:material-about-library:2.3.0'
    implementation "com.mikepenz:iconics-core:3.1.0"
    implementation 'com.mikepenz:material-design-iconic-typeface:2.2.0.5@aar'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.22.0'
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    androidTestImplementation 'androidx.test:rules:1.1.0-alpha4'

    androidTestImplementation('androidx.test:runner:1.1.0-alpha4') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4') {
        exclude module: 'support-annotations'
    }
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.1.0-alpha4') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestImplementation('com.jakewharton.espresso:okhttp3-idling-resource:1.0.1-SNAPSHOT') {
        exclude module: 'okio'
        exclude module: 'support-annotations'
        exclude module: 'okhttp'
    }

    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    androidTestImplementation "org.koin:koin-test:$koin_version"

    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'com.jakewharton.timber:timber:4.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha2'
}