import com.android.ddmlib.DdmPreferences

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'origin/master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim()) * 100 + 4000000
    }
    catch (ignored) {
        return -1
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null
    }
}

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.1'
    defaultConfig {
        applicationId "cz.kutner.comicsdbclient.comicsdbclient"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode getVersionCode()
        versionName getVersionName()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        DdmPreferences.setTimeOut(60000)
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            zipAlignEnabled true
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }

    lintOptions {
        lintConfig rootProject.file('lint.xml')
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile "com.android.support:appcompat-v7:$support_library_version"
    compile "com.android.support:cardview-v7:$support_library_version"
    compile "com.android.support:recyclerview-v7:$support_library_version"
    compile "com.android.support:design:$support_library_version"

    compile "com.google.firebase:firebase-core:$firebase_version"
    releaseCompile  "com.google.firebase:firebase-crash:$firebase_version"
    compile "com.google.firebase:firebase-perf:$firebase_version"

    compile 'com.pnikosis:materialish-progress:1.7'
    compile 'com.github.jkwiecien:Switcher:2.1.5'
    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:converter-gson:2.3.0'

    compile 'co.metalab.asyncawait:asyncawait:1.0.0'

    compile 'com.github.bumptech.glide:glide:4.0.0'
    compile 'com.github.bumptech.glide:okhttp3-integration:4.0.0@aar'

    compile 'com.squareup.okhttp3:okhttp:3.8.1'

    compile 'com.hannesdorfmann:adapterdelegates3:3.0.1'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile 'com.github.daniel-stoneuk:material-about-library:2.2.1'
    compile "com.mikepenz:iconics-core:2.9.3@aar"
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.3@aar'

    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.9.0'
    testCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile ('com.android.support.test:runner:1.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:3.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:3.0.0') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'appcompat-v7'
        exclude module: 'design'
    }
    androidTestCompile ('com.jakewharton.espresso:okhttp3-idling-resource:1.0.0') {
        exclude module: 'okio'
        exclude module: 'support-annotations'
        exclude module: 'okhttp'
    }

    compile 'org.koin:koin-android:0.2.2'
    compile 'org.koin:koin-android-support:0.2.2'

    compile 'com.github.chrisbanes:PhotoView:2.0.0'
    compile 'com.jakewharton.timber:timber:4.5.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    kapt 'com.android.databinding:compiler:3.0.0-beta3'
}

apply plugin: 'com.google.gms.google-services'

